import Golosinas.*

describe "Test de Golosinas" {
	
	test "¿bombon cambia su peso?" {
		const bombon = new Bombon()
		bombon.recibeMordisco()
		assert.equals(11, bombon.peso())
	}
	
	test "¿alfajor cambia su peso?" {
		const alfajor = new Alfajor()
		alfajor.recibeMordisco()
		assert.equals(240, alfajor.peso())
	}
	
	test "¿caramelo cambia su peso?" {
		const caramelo = new Caramelo(sabor = frutilla)
		caramelo.recibeMordisco()
		assert.equals(4, caramelo.peso())
	}
	
	test "¿chupetin cambia su peso si tiene menos de 2?" {
		const chupetin = new Chupetin()
		chupetin.peso(1.98)
		chupetin.recibeMordisco()
		assert.equals(1.98, chupetin.peso())
	}
	
	test "¿cambia el peso de la oblea un 50%?" {
		const oblea = new Oblea()
		oblea.recibeMordisco()
		assert.equals(125, oblea.peso())
	}
	
	test "¿cambia el peso de la oblea en 25%?" {
		const oblea = new Oblea()
		oblea.recibeMordisco()
		oblea.recibeMordisco()
		oblea.recibeMordisco()
		assert.equals(46.875, oblea.peso())
	}
	
	test "¿El peso de chocolatin cambia?" {
		const chocolatin = new Chocolatin()
		const bombon = new Bombon()
		chocolatin.peso(100)
		assert.equals(100, chocolatin.peso())
		chocolatin.recibeMordisco()
		bombon.recibeMordisco()
		assert.equals(11, bombon.peso())
		assert.equals(98, chocolatin.peso())
	}
	
	test "¿A chocolatin se le asigno un precio?" {
		const chocolatin = new Chocolatin()
		chocolatin.peso(100)
		assert.equals(50, chocolatin.precio())
	}
	
	test "¿El baño de golosina modifica su peso?" {
		const oblea = new Oblea()
		const pesoTest = oblea.peso() + 4
		const golosinaBaniada = new GolosinaBaniada(golosina = oblea)
		golosinaBaniada.peso()
		assert.equals(pesoTest, golosinaBaniada.peso())
	}
	
	test "Al morder el bañado, cambia su peso y el de la golosina" {
		const oblea = new Oblea()
		const golosinaBaniada = new GolosinaBaniada(golosina = oblea)
		golosinaBaniada.recibeMordisco()
		const pesoTest = 2 + oblea.peso()
		assert.equals(pesoTest, golosinaBaniada.peso())
	}
	
	test "¿La pastilla Tutti Frutti cambió de sabor?" {
		const pastillaTuttiFrutti = new PastillaTuttiFrutti()
		pastillaTuttiFrutti.recibeMordisco()
		assert.equals(chocolate, pastillaTuttiFrutti.sabor())
		pastillaTuttiFrutti.recibeMordisco()
		assert.equals(naranja, pastillaTuttiFrutti.sabor())
		pastillaTuttiFrutti.recibeMordisco()
		assert.equals(frutilla, pastillaTuttiFrutti.sabor())
	}
	
	test "El caramelo puede tener varios sabores" {
		const carameloFrutilla = new Caramelo(sabor = frutilla)
		assert.equals(frutilla, carameloFrutilla.sabor())
		const carameloNaranja = new Caramelo(sabor = naranja)
		assert.equals(naranja, carameloNaranja.sabor())
		const carameloChocolate = new Caramelo(sabor = chocolate)
		assert.equals(chocolate, carameloChocolate.sabor())
	}
	
	test "Puede haber bombones duros que se comporten distinto" {
		var bombon = new Bombon() 
		bombon.recibeMordisco() 
		assert.equals(11, bombon.peso()) 
		bombon = new BombonDuro() 
		bombon.recibeMordisco() 
		assert.equals(13.5, bombon.peso()) 
	}
	
	test "El caramelo relleno al ser mordido su sabor cambia a chocolate" {
		var caramelo = new Caramelo(sabor = frutilla) 
		caramelo.recibeMordisco()
		assert.equals(4, caramelo.peso())
		assert.equals(frutilla, caramelo.sabor())
		caramelo = new CarameloRelleno(sabor = naranja) 
		caramelo.recibeMordisco() 
		assert.equals(4, caramelo.peso())
		assert.equals(chocolate, caramelo.sabor())
	}
	
	test "La oblea crujiente en sus primeros 3 mordiscos pierde 5 gramos de peso adicional" {
		var oblea = new Oblea() 
		oblea.recibeMordisco()
		oblea.recibeMordisco()
		oblea.recibeMordisco() 
		assert.equals(46.875, oblea.peso())
		oblea.recibeMordisco()
		assert.equals((46.875 * 0.75), oblea.peso())
		oblea = new ObleaCrujiente() 
		oblea.recibeMordisco()
		oblea.recibeMordisco()
		oblea.recibeMordisco()
		assert.equals(43.5, oblea.peso())
		oblea.recibeMordisco()
		assert.equals((43.5 * 0.7), oblea.peso())
	}
}