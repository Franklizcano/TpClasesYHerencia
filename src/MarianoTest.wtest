import Mariano.*
import Golosinas.*

describe "Test de Mariano" {
	
	test "Mariano puede comprar una golosina" {
		const alfajor = new Alfajor()
        mariano.comprar(alfajor)
		assert.that(mariano.tieneLaGolosina(alfajor))
	}
	
	test "Mariano desecho una golosina" {
		const alfajor = new Alfajor()
		mariano.comprar(alfajor)
		mariano.desechar(alfajor)
		assert.that(mariano.comproYDesecho(alfajor))
	}
	
	test "Mariano comprueba si tiene golosinas sin TACC" {
		const oblea = new Oblea()
		mariano.comprar(oblea)
		assert.notThat(mariano.hayGolosinaSinTACC())
		const chupetin = new Chupetin()
		mariano.comprar(chupetin)
		assert.that(mariano.hayGolosinaSinTACC())
	}
	
	test "Mariano puede probar varias golosinas" {
		const oblea = new Oblea()
		const chupetin = new Chupetin()		
		mariano.comprar(oblea)
		mariano.comprar(chupetin)
		mariano.probarGolosinas()
		assert.equals(125, oblea.peso())
		assert.equals(6.3, chupetin.peso())
	}
	
	test "Mariano comprueba si compro golosinas a precios cuidados" {
		const chupetin = new Chupetin()
		mariano.comprar(chupetin)
		assert.that(mariano.preciosCuidados())
		const alfajor = new Alfajor()
		mariano.comprar(alfajor)
		assert.notThat(mariano.preciosCuidados())
	}
	
	test "Mariano comprueba si tiene golosinas de un sabor" {
		const chupetin = new Chupetin()
		mariano.comprar(chupetin)
		assert.equals(chupetin, mariano.golosinaDeSabor(naranja))
		const alfajor = new Alfajor()
		mariano.comprar(alfajor)
		assert.equals(alfajor, mariano.golosinaDeSabor(chocolate))
	}
	
	test "Mariano comprueba las golosinas que tiene de un sabor" {
		const alfajor = new Alfajor()
		mariano.comprar(alfajor)
		const chocolatin = new Chocolatin(peso = 5)
		mariano.comprar(chocolatin)
		assert.equals([alfajor, chocolatin], mariano.golosinasDeSabor(chocolate))
	}
	
	test "Mariano comprueba los sabores de las golosinas que compró" {
		const alfajor = new Alfajor()
		const chupetin = new Chupetin()
		const chocolatin = new Chocolatin(peso = 5)
		mariano.comprar(alfajor)
		mariano.comprar(chupetin)
		mariano.comprar(chocolatin)
		assert.equals(#{chocolate, naranja}, mariano.sabores())
	}
	
	test "Mariano comprueba cual es la golosina más cara de su bolsa" {
		const alfajor = new Alfajor()
		const chupetin = new Chupetin()
		const chocolatin = new Chocolatin(peso = 5)
		const oblea = new Oblea()
		mariano.comprar(alfajor)
		mariano.comprar(chupetin)
		mariano.comprar(chocolatin)
		mariano.comprar(oblea)
		assert.equals(alfajor, mariano.golosinaMasCara())
	}
	
	test "Mariano comprueba el peso de su bolsa" {
		const alfajor = new Alfajor()
		const chupetin = new Chupetin()
		const oblea = new Oblea()
		mariano.comprar(alfajor)
		mariano.comprar(chupetin)
		mariano.comprar(oblea)
		assert.equals(557, mariano.pesoGolosinas())
	}
	
	test "Mariano comprueba cuales golosinas deseadas le falta por comprar" {
		const alfajor = new Alfajor()
		const chupetin = new Chupetin()
		const oblea = new Oblea()
		mariano.comprar(alfajor)
		mariano.comprar(chupetin)
		assert.equals(#{oblea}, mariano.golosinasFaltantes([alfajor, chupetin, oblea]))
	}
	
	test "Mariano comprueba que sabores le falta comprar" {
		const caramelo = new Caramelo(sabor = chocolate)
		const oblea = new Oblea()
		mariano.comprar(oblea)
		mariano.comprar(caramelo)
		assert.equals(#{chocolate, naranja}, mariano.gustosFaltantes([frutilla, chocolate, naranja, vainilla]))
	}
	
	test "Mariano comprueba cuanto gastó en golosinas de un sabor" {
		const bombon = new Bombon()
		const pastillaTuttiFrutti = new PastillaTuttiFrutti()
		const oblea = new Oblea()
		mariano.comprar(bombon)
		mariano.comprar(oblea)
		mariano.comprar(pastillaTuttiFrutti)
		assert.equals(12, mariano.gastoEn(frutilla))
	}
	
	test "Mariano comprueba si desecho golosinas" {
		const bombon = new Bombon()
		const pastillaTuttiFrutti = new PastillaTuttiFrutti()
		const oblea = new Oblea()
		mariano.comprar(bombon)
		mariano.comprar(oblea)
		mariano.comprar(pastillaTuttiFrutti)
		mariano.desechar(bombon)
		mariano.desechar(oblea)
		assert.that(mariano.comproYDesecho(bombon))
		assert.that(mariano.comproYDesecho(oblea))
	}
	
	test "Mariano comprueba cual es el sabor más popular de su bolsa" {
		const bombon = new Bombon()
		const pastillaTuttiFrutti = new PastillaTuttiFrutti()
		const oblea = new Oblea()
		mariano.comprar(bombon)
		mariano.comprar(oblea)
		mariano.comprar(pastillaTuttiFrutti)
		assert.equals(frutilla, mariano.saborMasPopular())
	}
	
	test "Mariano comprueba cuales golosinas de un sabor son las mas pesadas" {
		const alfajor = new Alfajor()
		const bombon = new Bombon()
		const oblea = new Oblea()
		mariano.comprar(bombon)
		mariano.comprar(oblea)
		mariano.comprar(alfajor)
		assert.equals(chocolate, mariano.saborMasPesado())
	}
	
	test "Mariano puede bañar una golosina" {
		const alfajor = new Alfajor()
		const jorgitoBaniado = new GolosinaBaniada(golosina = alfajor)
		mariano.comprar(jorgitoBaniado)
		assert.that(mariano.tieneLaGolosina(jorgitoBaniado))
		assert.equals(304, jorgitoBaniado.peso())
	}
}